group 'com.github.jonathanxd'

apply plugin: 'java'
apply plugin: 'license'
apply plugin: 'maven-publish'

jar {
    manifest {
        attributes 'Implementation-Title': 'JwIUtils-Json-lang-loader',
                'Implementation-Version': project.version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation rootProject
    implementation "com.googlecode.json-simple:json-simple:1.1.1"
    compileOnly "org.jetbrains:annotations:15.0"
    testImplementation group: 'junit', name:'junit', version: '4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: classes) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

license {
    ext.name = 'JwIUtils-Json-lang-loader'
    ext.organization = 'TheRealBuggy/JonathanxD (https://github.com/JonathanxD/)'
    ext.description = 'Loader of json lang files'
    ext.url = 'https://github.com/JonathanxD/JwIUtils/'
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.email = 'jonathan.scripter@programmer.net'

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "**/*.json"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"
    exclude "**/ASM*.java"
    exclude "**/TypeTools*.java"

    header file('../LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    repositories {
        maven {
            name = "Local"
            // change to point to your repo, e.g. http://my.org/repo
            url = "$buildDir/repo"
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/JonathanxD/jwiutils"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
        maven {
            name = "GitLab"
            url "https://gitlab.com/api/v4/projects/28895078/packages/maven"
            credentials(HttpHeaderCredentials) {
                def ciToken = System.getenv("CI_JOB_TOKEN")
                if (ciToken != null && !ciToken.isEmpty()) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                } else {
                    name = "Private-Token"
                    value = project.findProperty("GITLAB_TOKEN") ?: System.getenv("GITLAB_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
        maven {
            name = "GitLabJgang"
            url "https://gitlab.com/api/v4/projects/30392813/packages/maven"
            credentials(HttpHeaderCredentials) {
                def ciToken = System.getenv("CI_JOB_TOKEN")
                if (ciToken != null && !ciToken.isEmpty()) {
                    name = "Job-Token"
                    value = System.getenv("CI_JOB_TOKEN")
                } else {
                    name = "Private-Token"
                    value = project.findProperty("GITLAB_TOKEN") ?: System.getenv("GITLAB_TOKEN")
                }
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId 'json-lang-loader'
            from components.java

            artifact sourcesJar
        }
    }
}