group 'com.github.jonathanxd'

apply plugin: 'java'
apply plugin: 'license'

jar {
    manifest {
        attributes 'Implementation-Title': 'JwIUtils-Properties',
                'Implementation-Version': project.version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation rootProject
    compileOnly "org.jetbrains:annotations:15.0"
    testImplementation group: 'junit', name:'junit', version: '4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: classes) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

license {
    ext.name = 'JwIUtils-Properties'
    ext.organization = 'TheRealBuggy/JonathanxD (https://github.com/JonathanxD/)'
    ext.description = 'Properties module of JwIUtils'
    ext.url = 'https://github.com/JonathanxD/JwIUtils/'
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.email = 'jonathan.scripter@programmer.net'

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"
    exclude "**/ASM*.java"
    exclude "**/TypeTools*.java"

    header file('../LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    repositories {
        maven {
            name = "Local"
            // change to point to your repo, e.g. http://my.org/repo
            url = "$buildDir/repo"
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/JonathanxD/jwiutils"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId 'properties'
            from components.java

            artifact sourcesJar
        }
    }
}